archetype (adl_version=1.4)
	openEHR-EHR-OBSERVATION.blood_pressure.v1

concept
	[at0000]	-- Blood Pressure
language
	original_language = <[ISO_639-1::en]>
	translations = <
		["fa"] = <
			language = <[ISO_639-1::fa]>
			author = <
				["name"] = <"Shahla Foozonkhah">
				["organisation"] = <"Ocean Informatics">
				["email"] = <"shahla.foozonkhah@oceaninformatics.com">
			>
		>
		["de"] = <
			language = <[ISO_639-1::de]>
			author = <
				["name"] = <"Sebastian Garde, Jasmin Buck">
				["organisation"] = <"Ocean Informatics, University of Heidelberg">
			>
		>
	>
description
	original_author = <
		["name"] = <"Sam Heard">
		["organisation"] = <"Ocean Informatics">
		["email"] = <"sam.heard@oceaninformatics.com">
		["date"] = <"22/03/2006">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record the systemic arterial blood pressure of an individual. ">
			use = <"Use to record all representations of systemic arterial blood pressure measurement, no matter which method or body location is used to record it. The archetype is intended to capture blood pressure measurements in all clinical scenarios - for example, self-measurement with a home blood pressure machine; an emergency assessment of systolic using palpation and a sphygmomanometer; measurements taken in clinical consultations or during exercise stress testing; and a series of measurements made by a machine in Intensive Care.   
There is a rich state model that supports interpretation of measurements through identifying patient position, exercise, confounding factors and angle of a tilt table in research. 
Named events have been limited to average over a 24 hour period, however templates can further constrain the default 'any event' to cater for specific requirements for blood pressure measurements such as recording Blood Pressure against specific points in time, or over a range of intervals (+/- mathematical functions).">
			keywords = <"observations", "measurement", "bp", "vital signs", "mean arterial pressure", "pulse pressure", "systolic", "diastolic", "RR", "NIBP">
			misuse = <"Not to be used for intravenous pressure.
Not to be used for the measurement of arterial blood pressure which is NOT a surrogate for arterial pressure in the systemic circulation eg specific measurement of right Pulmonary artery pressure.  
Use OBSERVATION.intravascular_pressure and related specialisations in both of these situations.">
			copyright = <"© openEHR Foundation">
		>
		["de"] = <
			language = <[ISO_639-1::de]>
			purpose = <"Dient der Dokumentation des systemischen arteriellen Blutdrucks einer Person.">
			use = <"Alle Blutdruckmessungen werden unter Zuhilfenahme dieses Archetypen dokumentiert, unabhängig davon, welche Methode oder Körperstelle zur Messung benutzt wurden. Der Archetyp dient der Dokumentation des Blutdrucks in alle klinischen Szenarien - z.B. durch eine Blutdruckmaschine zuhause; eine Notfallmessung durch Palpation und ein Sphygmomanometer; Messungen beim Hausarzt oder im Rahmen von Belastungstests; sowie einer Serie von Messungen durch eine Maschine auf der Intensivstation.
Der Archetyp beinhaltet ein umfassendes Status-Modell, das die Interpretation der Messung unterstützt, indem Position, Anstrengung, Einflussfaktoren, Neigungswinkel angegeben werden können.
Benannte Ereignisse wurden auf den 24-stündigen Durchschnitt beschränkt, jedoch können Templates jederzeit das standardmäßig vorhandene Ereignis ('any event') weiter einschränken, um spezifischen Anforderungen gerecht zu werden, wie z.B. der Messung zu bestimmten Zeitpunkten, oder über eine Anzahl von Intervallen (+/- mathematische Funktionen).">
			keywords = <"Beobachtungen", "Messungen", "Vitalparameter", "Mittlerer arterieller Druck", "Pulsdruck", "systolisch", "diastolisch", "RR", "Riva-Rocci", "NIBP">
			misuse = <"Nicht benutzen zur Dokumentation des intravenösen Drucks.
Nicht benutzen zur Dokumentation des arteriellen Blutdrucks, welcher KEIN Surrogat für den arteriellen Druck in der systemischen Zirkluation ist, z.B. die spezifische Messung des rechten pulmonaren Arteriendrucks.
In diesem Fall sollte der OBSERVATION.intravascular_pressure Archetyp bzw. dessen Spezialisierungen verwendet werden.">
			copyright = <"© openEHR Foundation">
		>
		["fa"] = <
			language = <[ISO_639-1::fa]>
			purpose = <"ثبت نمودن فشار خون وریدی کل یک فرد">
			use = <"برای ثبت هر چیز نشاندهنده اندازه گیری فشاره خون وریدی کل، جدا از ابنکه برای ثبت آن از چه شیوه ای یا کدام بخش از بدن استفاده شده باشد، بکار می رود. این الگوساز برای گردآوری اندازه گیری فشار خون در همه سناریوهای بالینی طراحی شده است، مانند اندازه گیری توسط خود فرد بوسیله دستگاه فشار خون خانگی، ارزیابی اورژانسی سیستولیک با لمس نبض و یک \"فشارسنج خون\"، اندازه گیریهای انجام گرفته در مشاوره بالینی یا حین تست ورزشی، و یا یک رشته از اندازه گیریهای دستگاه مراقبت های ویژه.
مدل حالت توانمندی وجود دارد که تفسیر اندازه گیری ها را از طریق شناسایی موقعیت بیمار، ورزش، فاکتورهای مبهم و تست تخت شیبدار، پشتیبانی می کند.
رویدادهای نام گذاری شده بگونه ای محدود شده اند که روی یک دوره 24 ساعته میانگین گیری کنند هر چند که الگو ها می توانند پیش فرض -هر گونه  رویداد- را برای فراهم کردن خواسته های خاص از اندازه گیری فشار خون، با محدودیتهایی نظیر ثبت فشار خون سر وقت یا روی رنجی از فاصله های زمانی (با عمل جمع یا منها)، بیشتر محدود کنند 
">
			keywords = <"مشاهدات", "اندازه گیری", "فشار خون", "علایم حیاتی", "میانگین فشار وریدی", "فشار نبض", "سیستولیک", "دیاستولیک", "تنفسهای سریع", "فشار خون غیر تهاجمی">
			misuse = <"برای فشار داخل وریدی استفاده نشود.
برای اندازه گیری فشار خون وریدی نباید استفاده شود که جایگزینی برای فشار وریدی در گردش خون کلی نیست، بعنوان مثال اندازه گیری خاص فشار وریدی ریه راست.

 و تخصص های مربوطه برای موارد بالا بکار رود\"OBSERVATION.intravascular_pressure\"
 
">
			copyright = <"© openEHR Foundation">
		>
	>
	lifecycle_state = <"AuthorDraft">
	other_contributors = <"Koray Atalag, University of Auckland, New Zealand", "Knut Bernstein, MEDIQ, Denmark", "Marja Buur, Medisch Centrum Alkmaar, Netherlands", "Rong Chen, Cambio Healthcare Systems, Sweden", "Beatriz de Faria Leão, Zilics, Brazil", "Paul Donaldson, Nursing Informatics Australia, Australia", "Jose Florez Arango, Universidad de Antioquia, Colombia", "Gerard Freriks, ERC, Netherlands", "Sebastian Garde, Ocean Informatics, Germany", "Anneke Goossen, Results 4 Care, Netherlands", "Sam Heard, Ocean Informatics, Australia", "Karsten Heusser, Hannover Medical School, Germany", "Omer Hotomaroglu, Turkey", "Evelyn Hovenga, EJSH Consulting, Australia", "Derek Hoy, United Kingdom", "Pieter Hummel, Medisch Centrum Alkmaar, Netherlands", "Eugene Igras, IRIS Systems, Inc., Canada", "Sundaresan Jagannathan, Scottish NHS, United Kingdom", "Andrew James, University of Toronto, Canada", "Heather Leslie, Ocean Informatics, Australia (Editor)", "Rikard Lovstrom, Swedish Medical Association, Sweden", "Rohan Martin, Ambulance Victoria, Australia", "Ian McNicoll, Ocean Informatics, United Kingdom", "Jeroen Meintjens, Medisch Centrum Alkmaar, Netherlands", "Udo Müller-Oest, CompuGROUP Software, Germany", "Melvin Reynolds, United Kingdom", "Tony Shannon, NHS, United Kingdom", "Hwei-Yee Tai, Tan Tock Seng Hospital, Singapore", "Stef Verlinden, Vivici, Netherlands", "Soon Ghee Yap, Singapore Health Services Pte Ltd, Singapore">
	other_details = <
		["MD5-CAM-1.0.1"] = <"D2C5F2928F1B4D9D717A6BA03CE968DF">
		["references"] = <"O'Brien E, Asmar R, Beilin L, et al. European Society of Hypertension recommendations for conventional, ambulatory and home blood pressure measurement. Journal of Hypertension [Internet]. 2003 [cited 2009 Jul 30] ; 21(5):821-848. Available from http://www.bhsoc.org/bp_monitors/ESH_BP_rec.pdf

Perloff D, Grim C, Flack J, Frohlich ED, Hill M, McDonald M, Morgenstern BZ. Human blood pressure determination by sphygmomanometry. Circulation [Internet]. 1993 [cited 2009 Jul 29] 88 (5): 2460.  Available from: http://circ.ahajournals.org/cgi/reprint/88/5/2460
">
	>

definition
	OBSERVATION[at0000] matches {	-- Blood Pressure
		data matches {
			HISTORY[at0001] matches {	-- history
				events cardinality matches {1..*; unordered} matches {
					EVENT[at0006] occurrences matches {0..*} matches {	-- any event
						data matches {
							ITEM_TREE[at0003] matches {	-- blood pressure
								items cardinality matches {0..*; unordered} matches {
									ELEMENT[at0004] occurrences matches {0..1} matches {	-- Systolic
										value matches {
											C_DV_QUANTITY <
												property = <[openehr::125]>
												list = <
													["1"] = <
														units = <"mm[Hg]">
														magnitude = <|0.0..<1000.0|>
														precision = <|0|>
													>
												>
											>
										}
									}
									ELEMENT[at0005] occurrences matches {0..1} matches {	-- Diastolic
										value matches {
											C_DV_QUANTITY <
												property = <[openehr::125]>
												list = <
													["1"] = <
														units = <"mm[Hg]">
														magnitude = <|0.0..<1000.0|>
														precision = <|0|>
													>
												>
											>
										}
									}
									ELEMENT[at1006] occurrences matches {0..1} matches {	-- Mean Arterial Pressure
										value matches {
											C_DV_QUANTITY <
												property = <[openehr::125]>
												list = <
													["1"] = <
														units = <"mm[Hg]">
														magnitude = <|0.0..<1000.0|>
														precision = <|0|>
													>
												>
											>
										}
									}
									ELEMENT[at1007] occurrences matches {0..1} matches {	-- Pulse Pressure
										value matches {
											C_DV_QUANTITY <
												property = <[openehr::125]>
												list = <
													["1"] = <
														units = <"mm[Hg]">
														magnitude = <|0.0..<1000.0|>
														precision = <|0|>
													>
												>
											>
										}
									}
									ELEMENT[at0033] occurrences matches {0..1} matches {	-- Comment
										value matches {
											DV_TEXT matches {*}
										}
									}
								}
							}
						}
						state matches {
							ITEM_TREE[at0007] matches {	-- state structure
								items cardinality matches {0..*; unordered} matches {
									ELEMENT[at0008] occurrences matches {0..1} matches {	-- Position
										value matches {
											DV_CODED_TEXT matches {
												defining_code matches {
													[local::
													at1000, 	-- Standing
													at1001, 	-- Sitting
													at1002, 	-- Reclining
													at1003, 	-- Lying
													at1014; 	-- Lying with tilt to left
													at1001]	-- assumed value
												}
											}
										}
									}
									ELEMENT[at1052] occurrences matches {0..1} matches {	-- Confounding factors
										value matches {
											DV_TEXT matches {*}
										}
									}
									allow_archetype CLUSTER[at1030] occurrences matches {0..1} matches {	-- Exertion  
										include
											archetype_id/value matches {/openEHR-EHR-CLUSTER\.level_of_exertion(-[a-zA-Z0-9_]+)*\.v1/}
									}
									ELEMENT[at1043] occurrences matches {0..1} matches {	-- Sleep status
										value matches {
											DV_CODED_TEXT matches {
												defining_code matches {
													[local::
													at1044, 	-- Alert & awake
													at1045; 	-- Sleeping
													at1044]	-- assumed value
												}
											}
										}
									}
									ELEMENT[at1005] occurrences matches {0..1} matches {	-- Tilt
										value matches {
											C_DV_QUANTITY <
												property = <[openehr::497]>
												list = <
													["1"] = <
														units = <"°">
														magnitude = <|-90.0..90.0|>
														precision = <|0|>
													>
												>
												assumed_value = <
													magnitude = <0.0>
													units = <"°">
													precision = <0>
												>
											>
										}
									}
								}
							}
						}
					}
					INTERVAL_EVENT[at1042] occurrences matches {0..1} matches {	-- 24 hour average 
						math_function matches {
							DV_CODED_TEXT matches {
								defining_code matches {[openehr::146]}
							}
						}
						width matches {
							DV_DURATION matches {
								value matches {|PT24H|}
							}
						}
						data matches {
							use_node ITEM_TREE /data[at0001]/events[at0006]/data[at0003]	-- /data[history]/events[any event]/data[blood pressure]
						}
						state matches {
							use_node ITEM_TREE /data[at0001]/events[at0006]/state[at0007]	-- /data[history]/events[any event]/state[state structure]
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0011] matches {	-- list structure
				items cardinality matches {0..*; unordered} matches {
					ELEMENT[at0013] occurrences matches {0..1} matches {	-- Cuff size
						value matches {
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at0015, 	-- Adult Thigh
									at0016, 	-- Large Adult
									at0017, 	-- Adult
									at1008, 	-- Small Adult
									at1009, 	-- Paediatric/Child
									at1018, 	-- Infant
									at1019]	-- Neonatal
								}
							}
						}
					}
					CLUSTER[at1033] occurrences matches {0..1} matches {	-- Location
						items cardinality matches {1..*; unordered} matches {
							ELEMENT[at0014] occurrences matches {0..1} matches {	-- Location of measurement
								value matches {
									DV_CODED_TEXT matches {
										defining_code matches {
											[local::
											at0025, 	-- Right arm
											at0026, 	-- Left arm
											at0027, 	-- Right thigh
											at0028, 	-- Left thigh
											at1020, 	-- Right wrist
											at1021, 	-- Left wrist
											at1026, 	-- Right ankle
											at1031, 	-- Left ankle
											at1032, 	-- Finger
											at1051, 	-- Toe
											at1053]	-- Intra-arterial
										}
									}
								}
							}
							ELEMENT[at1034] occurrences matches {0..1} matches {	-- Specific location
								value matches {
									DV_TEXT matches {*}
								}
							}
						}
					}
					ELEMENT[at1035] occurrences matches {0..1} matches {	-- Method
						value matches {
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at1036, 	-- Auscultation
									at1037, 	-- Palpation
									at1039, 	-- Machine
									at1040]	-- Invasive
								}
							}
						}
					}
					ELEMENT[at1038] occurrences matches {0..1} matches {	-- Mean Arterial Pressure Formula
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at1010] occurrences matches {0..1} matches {	-- Diastolic endpoint
						value matches {
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at1011, 	-- Phase IV
									at1012]	-- Phase V
								}
							}
						}
					}
					allow_archetype CLUSTER[at1025] occurrences matches {0..1} matches {	-- Device
						include
							archetype_id/value matches {/openEHR-EHR-CLUSTER\.device(-[a-zA-Z0-9_]+)*\.v1/}
					}
				}
			}
		}
	}



ontology
	terminologies_available = <"SNOMED-CT", ...>
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Blood Pressure">
					description = <"The local measurement of arterial blood pressure which is a surrogate for arterial. pressure in the systemic circulation.  Most commonly, use of the term 'blood pressure' refers to measurement of brachial artery pressure in the upper arm.">
				>
				["at0001"] = <
					text = <"history">
					description = <"history Structural node">
				>
				["at0003"] = <
					text = <"blood pressure">
					description = <"@ internal @">
				>
				["at0004"] = <
					text = <"Systolic">
					description = <"Peak systemic arterial blood pressure  - measured in systolic or contraction phase of the heart cycle.">
				>
				["at0005"] = <
					text = <"Diastolic">
					description = <"Minimum systemic arterial blood pressure - measured in the diastolic or relaxation phase of the heart cycle.">
				>
				["at0006"] = <
					text = <"any event">
					description = <"Default event">
				>
				["at0007"] = <
					text = <"state structure">
					description = <"@ internal @">
				>
				["at0008"] = <
					text = <"Position">
					description = <"The position of the subject at the time of measurement.">
				>
				["at0011"] = <
					text = <"list structure">
					description = <"list structure">
				>
				["at0013"] = <
					text = <"Cuff size">
					description = <"The size of the cuff used for blood pressure measurement.  ">
					comment = <"Perloff D, Grim C, Flack J, Frohlich ED, Hill M, McDonald M, Morgenstern BZ. Human blood pressure determination by sphygmomanometry. Circulation 1993;88;2460-2470. ">
				>
				["at0014"] = <
					text = <"Location of measurement">
					description = <"Common body sites where blood pressure is recorded.">
				>
				["at0015"] = <
					text = <"Adult Thigh">
					description = <"A cuff used for an adult thigh - bladder approx 20cm x 42cm.">
				>
				["at0016"] = <
					text = <"Large Adult">
					description = <"A cuff for adults with larger arms - bladder approx 16cm x 38cm.">
				>
				["at0017"] = <
					text = <"Adult">
					description = <"A cuff that is standard for an adult - bladder approx 13cm x 30cm.">
				>
				["at0025"] = <
					text = <"Right arm">
					description = <"The right arm of the person.">
				>
				["at0026"] = <
					text = <"Left arm">
					description = <"The left arm of the person.">
				>
				["at0027"] = <
					text = <"Right thigh">
					description = <"The right thigh of the person.">
				>
				["at0028"] = <
					text = <"Left thigh">
					description = <"The left thigh of the person.">
				>
				["at0033"] = <
					text = <"Comment">
					description = <"Comment on blood pressure measurement.">
				>
				["at1000"] = <
					text = <"Standing">
					description = <"Standing at the time of blood pressure measurement.">
				>
				["at1001"] = <
					text = <"Sitting">
					description = <"Sitting (for example on bed or chair) at the time of blood pressure measurement.">
				>
				["at1002"] = <
					text = <"Reclining">
					description = <"Reclining at the time of blood pressure measurement.">
				>
				["at1003"] = <
					text = <"Lying">
					description = <"Lying flat at the time of blood pressure measurement.">
				>
				["at1005"] = <
					text = <"Tilt">
					description = <"The craniocaudal tilt of the surface on which the person is lying at the time of measurement.">
				>
				["at1006"] = <
					text = <"Mean Arterial Pressure">
					description = <"The average arterial pressure that occurs over the entire course of the heart contraction and relaxation cycle.">
				>
				["at1007"] = <
					text = <"Pulse Pressure">
					description = <"The difference between the systolic and diastolic pressure.">
				>
				["at1008"] = <
					text = <"Small Adult">
					description = <"A cuff used for a small adult - bladder approx 10cm x 24cm.">
				>
				["at1009"] = <
					text = <"Paediatric/Child">
					description = <"A cuff that is appropriate for a child or adult with a thin arm - bladder approx 8cm x 21cm.">
				>
				["at1010"] = <
					text = <"Diastolic endpoint">
					description = <"Record which Korotkoff sound is used for determining diastolic pressure using auscultative method.">
				>
				["at1011"] = <
					text = <"Phase IV">
					description = <"The fourth Korotkoff sound is identified as an abrupt muffling of sounds.">
				>
				["at1012"] = <
					text = <"Phase V">
					description = <"The fifth Korotkoff sound is identified by absence of sounds as the cuff pressure drops below the diastolic blood pressure.">
				>
				["at1014"] = <
					text = <"Lying with tilt to left">
					description = <"Lying flat with some lateral tilt, usually angled towards the left side.   Commonly required in the last trimester of pregnancy to relieve aortocaval compression.">
				>
				["at1018"] = <
					text = <"Infant">
					description = <"A cuff used for infants - bladder approx 5cm x 15cm.">
				>
				["at1019"] = <
					text = <"Neonatal">
					description = <"A cuff used for a neonate, assuming cuff is the appropriate size for maturity and birthweight of the neonate.">
				>
				["at1020"] = <
					text = <"Right wrist">
					description = <"The right wrist of the subject.">
				>
				["at1021"] = <
					text = <"Left wrist">
					description = <"The left wrist of the subject.">
				>
				["at1025"] = <
					text = <"Device">
					description = <"Details about sphygmomanometer or other device used to measure the blood pressure.">
				>
				["at1026"] = <
					text = <"Right ankle">
					description = <"The right ankle of the subject.">
				>
				["at1030"] = <
					text = <"Exertion  ">
					description = <"Details about physical activity undertaken at the time of blood pressure.measurement.">
				>
				["at1031"] = <
					text = <"Left ankle">
					description = <"The left ankle of the subject.">
				>
				["at1032"] = <
					text = <"Finger">
					description = <"A finger of the subject.  Identification of the finger can be recorded in 'Specific Location' data element, if required.">
				>
				["at1033"] = <
					text = <"Location">
					description = <"Body location where blood pressure is measured.  Use 'Location of measurement' to select from common sites.  Use 'Specific location' to record more specific details or a site that is not in the common set or to refer to an external terminology.">
				>
				["at1034"] = <
					text = <"Specific location">
					description = <"Specific details about the body site where blood pressure is recorded.">
				>
				["at1035"] = <
					text = <"Method">
					description = <"Method of measurement of blood pressure.">
				>
				["at1036"] = <
					text = <"Auscultation">
					description = <"Method of measuring blood pressure externally, using a stethoscope and Korotkoff sounds.">
				>
				["at1037"] = <
					text = <"Palpation">
					description = <"Method of measuring blood pressure externally, using palpation (usually of the brachial or radial arteries).">
				>
				["at1038"] = <
					text = <"Mean Arterial Pressure Formula">
					description = <"Formula used to calculate the MAP (if recorded in data).">
				>
				["at1039"] = <
					text = <"Machine">
					description = <"Method of measuring blood pressure externally, using a blood pressure machine.">
				>
				["at1040"] = <
					text = <"Invasive">
					description = <"Method of measuring blood pressure internally ie involving penetration of the skin and measuring inside blood vessels.">
				>
				["at1042"] = <
					text = <"24 hour average ">
					description = <"Estimate of the average blood pressure over a 24 hour period.">
				>
				["at1043"] = <
					text = <"Sleep status">
					description = <"Sleep status - supports interpretation of 24 hour ambulatory blood pressure records. ">
				>
				["at1044"] = <
					text = <"Alert & awake">
					description = <"Subject is fully conscious.">
				>
				["at1045"] = <
					text = <"Sleeping">
					description = <"Subject is in the natural state of bodily rest.">
				>
				["at1051"] = <
					text = <"Toe">
					description = <"A toe of the subject.   Identification of the toe can be recorded in 'Specific Location' data element, if required.">
				>
				["at1052"] = <
					text = <"Confounding factors">
					description = <"Comment on and record other incidental factors that may be contributing to the blood pressure measurement.  For example, level of anxiety or 'white coat syndrome'; pain or fever; changes in atmospheric pressure etc.">
				>
				["at1053"] = <
					text = <"Intra-arterial">
					description = <"Invasive measurement via transducer access line within an artery. Location of the transducer can be recorded in 'Specific Location' data element, if required.">
				>
			>
		>
		["de"] = <
			items = <
				["at0000"] = <
					text = <"Blutdruck">
					description = <"Die lokale Messung des arteriellen Blutdrucks als Surrogat für den arteriellen Druck in der systemischen Zirkulation. Häufig wird der Ausdruck 'Blutdruck' zur Bezeichung der Messung des brachialen Ateriendrucks im Oberarm verwendet.">
				>
				["at0001"] = <
					text = <"Historie">
					description = <"Historie">
				>
				["at0003"] = <
					text = <"Blutdruck">
					description = <"*@ internal @(en)">
				>
				["at0004"] = <
					text = <"Systolisch">
					description = <"Der höchste arterielle Blutdruck eines Zyklus - gemessen in der systolischen oder Kontraktionsphase des Herzens.">
				>
				["at0005"] = <
					text = <"Diastolisch">
					description = <"Der minimale systemische arterielle Blutdruck eines Zyklus - gemessen in der diastolischen oder Entspannungsphase des Herzens.">
				>
				["at0006"] = <
					text = <"any event">
					description = <"Default event">
				>
				["at0007"] = <
					text = <"*state structure(en)">
					description = <"*@ internal @(en)">
				>
				["at0008"] = <
					text = <"Position">
					description = <"Die Position der untersuchten Person während der Messung.">
				>
				["at0011"] = <
					text = <"Listenstruktur">
					description = <"Listenstruktur">
				>
				["at0013"] = <
					text = <"Manschettengröße">
					description = <"Die Größe der Manschette, die zur Blutdruckmessung benutzt wurde.">
					comment = <"Perloff D, Grim C, Flack J, Frohlich ED, Hill M, McDonald M, Morgenstern BZ. Human blood pressure determination by sphygmomanometry. Circulation 1993;88;2460-2470.">
				>
				["at0014"] = <
					text = <"Körperstelle der Messung">
					description = <"Gewöhnliche Körperstellen, an denen der Blutdruck gemessen wird.">
				>
				["at0015"] = <
					text = <"Oberschenkel eines Erwachsenen">
					description = <"Eine Manschette für den Oberschenkel eines Erwachsenen - ca. 20cm x 42cm.">
				>
				["at0016"] = <
					text = <"Großer Erwachsener">
					description = <"Eine Manschette für Erwachsene mit größeren Armen - ca. 16cm x 38cm.">
				>
				["at0017"] = <
					text = <"Erwachsener">
					description = <"Eine Standard-Manschette für einen Erwachsenen - ca. 13cm x 30cm.">
				>
				["at0025"] = <
					text = <"Rechter Arm">
					description = <"Der rechte Arm der Person.">
				>
				["at0026"] = <
					text = <"Linker Arm">
					description = <"Der linke Arm der Person.">
				>
				["at0027"] = <
					text = <"Rechter Oberschenkel">
					description = <"Der rechte Oberschenkel der Person.">
				>
				["at0028"] = <
					text = <"Linker Oberschenkel">
					description = <"Der linke Oberschenkel der Person.">
				>
				["at0033"] = <
					text = <"Kommentar">
					description = <"Kommentar zur Blutdruckmessung.">
				>
				["at1000"] = <
					text = <"Stehend">
					description = <"Stehend zum Zeitpunkt der Blutdruckmessung.">
				>
				["at1001"] = <
					text = <"Sitzend">
					description = <"Sitzend zum Zeitpunkt der Blutdruckmessung (z.B. auf einem Bett oder Stuhl).">
				>
				["at1002"] = <
					text = <"Zurückgelehnt">
					description = <"Patient zurückgelehnt zum Zeitpunkt der Blutdruckmessung.">
				>
				["at1003"] = <
					text = <"Liegend">
					description = <"Flach liegend zum Zeitpunkt der Blutdruckmessung.">
				>
				["at1005"] = <
					text = <"Neigung">
					description = <"Die cranio-kaudale Neigung der Oberfläche auf der die Person zum Zeitpunkt der Messung liegt.">
				>
				["at1006"] = <
					text = <"Mittlerer arterieller Druck">
					description = <"Der mittlerer arterielle Druck über den gesamten Verlauf der Konktraktions- und Entspannungsphase des Herzens.">
				>
				["at1007"] = <
					text = <"Pulsdruck">
					description = <"Der Abstand zwischen dem systolischen und dem diastolischen Blutdruckwert.">
				>
				["at1008"] = <
					text = <"Kleiner Erwachsener">
					description = <"Eine Manschette für einen kleinen Erwachsenen - ca. 10cm x 24cm.">
				>
				["at1009"] = <
					text = <"Pädiatrisch/Kind">
					description = <"Eine Manschette für ein Kind oder auch einen Erwachsenen mit einem schmalen Arm - 8cm x 21cm.">
				>
				["at1010"] = <
					text = <"Diastolischer Endpunkt">
					description = <"Dient der Dokumentation des Korotkoff-Geräusches, das verwendet wird, um bei auskultarischer Messung den diastolischen Blutdruck zu bestimmen.">
				>
				["at1011"] = <
					text = <"Phase IV">
					description = <"Das 4. Korotkoff-Geräusch - Die Geräusche klingen plätzlich gedämpft.">
				>
				["at1012"] = <
					text = <"Phase V">
					description = <"Das 5. Korotkoff-Geräusch - die Geräusche verschwinden völlig während der Manschettendruck unter den diastolischen Blutdruck fällt.">
				>
				["at1014"] = <
					text = <"Nach links geneigt liegend">
					description = <"Flach liegend mit seitlicher Neigung, normalerweise zur linken Seite. Häufig verwendet im letzten Drittel eine Schwangerschaft, um aorto-cavale Kompression zu vermeiden.">
				>
				["at1018"] = <
					text = <"Kleinkind">
					description = <"Eine Manschette für Kleinkinder und Säuglinge - ca. 5cm x 15cm.">
				>
				["at1019"] = <
					text = <"Neonatal">
					description = <"Eine Manschette für Neugeborene mit passender Größe für die Reife und das Geburtsgewicht des Neugeborenen.">
				>
				["at1020"] = <
					text = <"Rechtes Handgelenk">
					description = <"Das rechte Handgelenk der Person.">
				>
				["at1021"] = <
					text = <"Linkes Handgelenk">
					description = <"Das linke Handgelenk der Person.">
				>
				["at1025"] = <
					text = <"Gerät">
					description = <"Details über das Sphygmomanometer oder ein anderes Gerät, dass zur Blutdruckmessung verwendet wird.">
				>
				["at1026"] = <
					text = <"Rechtes Fußgelenk">
					description = <"Das rechte Fußgelenk der Person.">
				>
				["at1030"] = <
					text = <"Anstrengung">
					description = <"Details über physische Aktivitäten zur Zeit der Blutdruckmessung.">
				>
				["at1031"] = <
					text = <"Linkes Fußgelenk">
					description = <"Das linke Fußgelenk der Person">
				>
				["at1032"] = <
					text = <"Finger">
					description = <"Ein Finger der Person. Der Finger kann, falls benötigt, als 'Spezifische Stelle' genauer identifiziert werden.">
				>
				["at1033"] = <
					text = <"Körperstelle">
					description = <"Körperstelle der Blutdruckmessung. 'Körperstelle der Messung' kann für die gewöhnlichen Stellen verwendet werden. 'Spezifische Stelle' wird für die Aufzeichnung spezifischer Details einer Körperstelle, die nicht in den gewöhnlichen Stellen enthalten ist, verwendet, Sie kann auch verwendet werden, um auf eine externe Teminologie zu verweisen.">
				>
				["at1034"] = <
					text = <"Spezifische Stelle">
					description = <"Spezifische Details über die Körperstelle, an der der Blutdruck gemessen wird.">
				>
				["at1035"] = <
					text = <"Methode">
					description = <"Methode der Messung des Blutdrucks.">
				>
				["at1036"] = <
					text = <"Auskultation">
					description = <"Auskulatorische Messung unter Benutzung eines Stethoskops und der Korotkoff-Geräusche.">
				>
				["at1037"] = <
					text = <"Palpation">
					description = <"Palpatorische Messung, normalerweise an den brachialen oder radialen Arterien.">
				>
				["at1038"] = <
					text = <"Formel für mittleren arterieller Druck">
					description = <"Die Formel die ggf. verwendet wurde, um den mittleren arteriellen Druck zu berechnen.">
				>
				["at1039"] = <
					text = <"Machine">
					description = <"Messung durch eine Blutdruckmaschine.">
				>
				["at1040"] = <
					text = <"Invasiv">
					description = <"Invasive Messung des Blutdrucks innerhalb eines Gefäßes.">
				>
				["at1042"] = <
					text = <"24 Stunden Durchschnitt">
					description = <"Schätzung des durchschnittlichen Blutdrucks über eine 24-stündigen Zeitraum.">
				>
				["at1043"] = <
					text = <"Schlafzustand">
					description = <"Schlafzustand  - unterstützt die Auswertung von 24-stündigen ambulanten Aufzeichnungen des Blutdrucks.">
				>
				["at1044"] = <
					text = <"Aufmerksam und wach">
					description = <"Die untersuchte Person ist bei vollem Bewusstsein.">
				>
				["at1045"] = <
					text = <"Schlafend">
					description = <"Die untersuchte Person schläft.">
				>
				["at1051"] = <
					text = <"Zeh">
					description = <"Ein Zeh der Person. Der Zeh kann, falls benötigt, als 'Spezifische Stelle' genauer identifiziert werden.">
				>
				["at1052"] = <
					text = <"Einflussfaktoren">
					description = <"Kommentar und Aufzeichung anderer Faktoren die ggf. zu dem Ergebnis der Blutdruckmessung beitragen können. Die kann z.B. bei belastenden Situationen der Fall sein (z.B. sog. Weißkittelhypertonie, Schmerzen, Fieber, Änderungen im atmosphärischen Druck).">
				>
				["at1053"] = <
					text = <"Intra-arteriell">
					description = <"Invasive Messung über einen arteriellen Drucksensor. Die genaue Stelle des Drucksensors kann, falls benötigt, als 'Spezifische Stelle' genauer identifiziert werden.">
				>
			>
		>
		["fa"] = <
			items = <
				["at0000"] = <
					text = <"فشار خون">
					description = <"اندازه گیری موضعی فشار خون وریدی، که جایگزینی برای فشار وریدی در گردش خون کلی است.
معمولا واژه \"فشار خون\" به اندازه گیری فشار ورید بازویی در روی بازو گفته می شود">
				>
				["at0001"] = <
					text = <"تاریخچه">
					description = <"گره ساختاری تاریخچه ">
				>
				["at0003"] = <
					text = <"*blood pressure(en)">
					description = <"*@ internal @(en)">
				>
				["at0004"] = <
					text = <"سیستولیک">
					description = <"اوج فشار خون وریدی کلی که در سیستولیک یا فاز انقباضی چرخه گردش خون اندازه گیری می شود ">
				>
				["at0005"] = <
					text = <"دیاستولیک">
					description = <"حداقل فشار خون وریدی کلی که در دیاستولیک یا فاز انبساطی چرخه گردش خون اندازه گیری می شود">
				>
				["at0006"] = <
					text = <"هر رویداد">
					description = <"رویداد پیش فرض">
				>
				["at0007"] = <
					text = <"*state structure(en)">
					description = <"*@ internal @(en)">
				>
				["at0008"] = <
					text = <"موقعیت">
					description = <"موقعیت فرد در زمان اندازه گیری ">
				>
				["at0011"] = <
					text = <"ساختار لیست">
					description = <"ساختار لیست ">
				>
				["at0013"] = <
					text = <"اندازه کاف">
					description = <"اندازه کاف استفاده شده برای اندازه گیری فشار خون">
				>
				["at0014"] = <
					text = <"*Location of measurement(en)">
					description = <"*Common body sites where blood pressure is recorded.(en)">
				>
				["at0015"] = <
					text = <"ران بزرگسال">
					description = <"کاف استفاده شده در ران بزرگسال -اندازه مثانه 20 سانتی متر در 42 سانتی متر">
				>
				["at0016"] = <
					text = <"بزرگسال درشت">
					description = <"کاف بزرگسالان با برآمدگی بازوی بزرگتر در حدود 16 در 38 سانتیمتر">
				>
				["at0017"] = <
					text = <"*Adult(en)">
					description = <"*A cuff that is standard for an adult - bladder approx 13cm x 30cm.(en)">
				>
				["at0025"] = <
					text = <"*Right arm(en)">
					description = <"*The right arm of the person.(en)">
				>
				["at0026"] = <
					text = <"*Left arm(en)">
					description = <"*The left arm of the person.(en)">
				>
				["at0027"] = <
					text = <"*Right thigh(en)">
					description = <"*The right thigh of the person.(en)">
				>
				["at0028"] = <
					text = <"*Left thigh(en)">
					description = <"*The left thigh of the person.(en)">
				>
				["at0033"] = <
					text = <"نظر">
					description = <"نظر در مورد اندازه گیری فشار خون">
				>
				["at1000"] = <
					text = <"ایستاده">
					description = <"ایستاده در زمان اندازه گیری فشار خون">
				>
				["at1001"] = <
					text = <"نشسته">
					description = <"نشسته ( به عنوان مثال روی تخت یا صندلی) در زمان اندازه گیری فشار خون">
				>
				["at1002"] = <
					text = <"خمیده">
					description = <"خمیده در زمان اندازه گیری فشار خون">
				>
				["at1003"] = <
					text = <"خوابیده">
					description = <"خوابیده در زمان اندازه گیری فشار خون">
				>
				["at1005"] = <
					text = <"شیب">
					description = <"شیب طولی سطحی که فرد در حین اندازه گیری رون آن دراز کشیده است">
				>
				["at1006"] = <
					text = <"میانگین فشار وریدی">
					description = <"متوسط فشار خون وریدی که در کل دوره انقباظ و انبساط قلبی رخ می دهد">
				>
				["at1007"] = <
					text = <"فشار نبضی">
					description = <"تفاوت بین فشار سیستولیک و دیاستولیک">
				>
				["at1008"] = <
					text = <"*Small Adult(en)">
					description = <"*A cuff used for a small adult - bladder approx 10cm x 24cm.(en)">
				>
				["at1009"] = <
					text = <"*Paediatric/Child(en)">
					description = <"*A cuff that is appropriate for a child or adult with a thin arm - bladder approx 8cm x 21cm.(en)">
				>
				["at1010"] = <
					text = <"*Diastolic endpoint(en)">
					description = <"*Record which Korotkoff sound is used for determining diastolic pressure using auscultative method.(en)">
				>
				["at1011"] = <
					text = <"*Phase IV(en)">
					description = <"*The fourth Korotkoff sound is identified as an abrupt muffling of sounds.(en)">
				>
				["at1012"] = <
					text = <"*Phase V(en)">
					description = <"*The fifth Korotkoff sound is identified by absence of sounds as the cuff pressure drops below the diastolic blood pressure.(en)">
				>
				["at1014"] = <
					text = <"خوابیده  به چپ">
					description = <"خوابیدن صاف با کمی تمایل به یک سمت، معمولا به جهت چپ میل کرده و عموما در سه ماهه آخر حاملگی برای تسکین فشار آئورت نیاز به آن است">
				>
				["at1018"] = <
					text = <"*Infant(en)">
					description = <"*A cuff used for infants - bladder approx 5cm x 15cm.(en)">
				>
				["at1019"] = <
					text = <"*Neonatal(en)">
					description = <"*A cuff used for a neonate, assuming cuff is the appropriate size for maturity and birthweight of the neonate.(en)">
				>
				["at1020"] = <
					text = <"*Right wrist(en)">
					description = <"*The right wrist of the subject.(en)">
				>
				["at1021"] = <
					text = <"*Left wrist(en)">
					description = <"*The left wrist of the subject.(en)">
				>
				["at1025"] = <
					text = <"*Device(en)">
					description = <"*Details about sphygmomanometer or other device used to measure the blood pressure.(en)">
				>
				["at1026"] = <
					text = <"*Right ankle(en)">
					description = <"*The right ankle of the subject.(en)">
				>
				["at1030"] = <
					text = <"تقلا">
					description = <"جزییاتی درباره فعالیت فیزیکی انجام شده در زمان اندازه گیری فشار خون ">
				>
				["at1031"] = <
					text = <"*Left ankle(en)">
					description = <"*The left ankle of the subject.(en)">
				>
				["at1032"] = <
					text = <"*Finger(en)">
					description = <"*A finger of the subject.  Identification of the finger can be recorded in 'Specific Location' data element, if required.(en)">
				>
				["at1033"] = <
					text = <"*Location(en)">
					description = <"*Body location where blood pressure is measured.  Use 'Location of measurement' to select from common sites.  Use 'Specific location' to record more specific details or a site that is not in the common set or to refer to an external terminology.(en)">
				>
				["at1034"] = <
					text = <"*Specific location(en)">
					description = <"*Specific details about the body site where blood pressure is recorded.(en)">
				>
				["at1035"] = <
					text = <"*Method(en)">
					description = <"*Method of measurement of blood pressure.(en)">
				>
				["at1036"] = <
					text = <"*Auscultation(en)">
					description = <"*Method of measuring blood pressure externally, using a stethoscope and Korotkoff sounds.(en)">
				>
				["at1037"] = <
					text = <"*Palpation(en)">
					description = <"*Method of measuring blood pressure externally, using palpation (usually of the brachial or radial arteries).(en)">
				>
				["at1038"] = <
					text = <"*Mean Arterial Pressure Formula(en)">
					description = <"*Formula used to calculate the MAP (if recorded in data).(en)">
				>
				["at1039"] = <
					text = <"*Machine(en)">
					description = <"*Method of measuring blood pressure externally, using a blood pressure machine.(en)">
				>
				["at1040"] = <
					text = <"*Invasive(en)">
					description = <"*Method of measuring blood pressure internally ie involving penetration of the skin and measuring inside blood vessels.(en)">
				>
				["at1042"] = <
					text = <"میانگین 24 ساعته">
					description = <"برآورد میانگین فشار خون در دوره زمانی 24 ساعته">
				>
				["at1043"] = <
					text = <"وضعیت خواب">
					description = <"وضعیت خواب- به تفسیر ثبتهای صورت گرفته از فشار خون در خانه در طول 24 ساعت کمک می کند ">
				>
				["at1044"] = <
					text = <"هشیار و بیدار">
					description = <"فرد کاملا به هوش است ">
				>
				["at1045"] = <
					text = <"خوابیده">
					description = <"فرد در حالت طبیعی استراحت بدنی است">
				>
				["at1051"] = <
					text = <"*Toe(en)">
					description = <"*A toe of the subject.   Identification of the toe can be recorded in 'Specific Location' data element, if required.(en)">
				>
				["at1052"] = <
					text = <"عوامل مبهم">
					description = <"نظردهی و ثبت سایر عوامل ضمنی که ممکن است به اندازه گیری فشار خون کمک کنند . به عنوان مثال سطح اضطراب یا \"سندرم روپوش سفید\"، درد یا تب ، تغییرات فشار جوی و غیره ">
				>
				["at1053"] = <
					text = <"*Intra-arterial(en)">
					description = <"*Invasive measurement via transducer access line within an artery. Location of the transducer can be recorded in 'Specific Location' data element, if required.(en)">
				>
			>
		>
	>
	term_bindings = <
		["SNOMED-CT"] = <
			items = <
				["at0000"] = <[SNOMED-CT(2003)::163020007]>
				["at0004"] = <[SNOMED-CT(2003)::163030003]>
				["at0005"] = <[SNOMED-CT(2003)::163031004]>
				["at0013"] = <[SNOMED-CT(2003)::246153002]>
			>
		>
	>
